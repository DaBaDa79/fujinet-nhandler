 NEW

 REM SEE IF EXTENSIONS HAVE ALREADY BEEN INSTALLED.  NO NEED TO 
 REM LOAD THEM MORE THAN ONCE

 10 GOSUB 10000

 REM SET UP A PORT AND WAIT FOR A CONNECTION

 100 PRINT "ECHO SERVER STARTED." 
 110 PRINT "LISTENING FOR CONNECTION..."
 120 F$="N:TCP://:6502/"
 130 R=4:W=8
 130 F=0: RW=R+W: TR=0
 140 PRINT "OPEN ";F$
 150 & NOPEN F,RW,TR,F$
 
 REM WAIT FOR CONNECTION, WHEN CN IS NOT ZERO, THEN CONNECTION WILL BE DETECTED

 200 & NSTATUS F, BW, CN, ER
 210 IF CN = 0 THEN 200

 250 PRINT "CONNECTION DETECTED! (";CN;")"
 
 REM ACCEPT THE CONNECTION!
 
 300 & NACCEPT F
 310 PRINT "ACCEPTED CONNECTION."

 REM SEE IF THERE IS SOMETHING TO READ
 400 & NSTATUS F, BW, CN, ER

 REM IF WE'VE LOST THE CONNECTION, THEN QUIT
 410 IF CN = 0 THEN 500 

 REM IF THERE ARE NO CHARACTERS TO READ, THEN WAIT UNTIL THERE IS
 420 IF BW = 0 THEN 400

 REM READ THE CHARACTER AND SEND IT OUT
 430 & NREAD F, A$, 1
 440 PRINT A$;
 450 & NWRITE  F, A$, 1

 460 GOTO 400

 500 PRINT:PRINT "DISCONNECTED.": & NCLOSE F
 510 GOTO 110

 1000 END 


10000 REM *************************
10010 REM DETERMINE IF FUJI EXTENSIONS ARE INSTALLED
10020 REM *************************
10022 GOSUB 10030: IF R=1 THEN PRINT "EXTENSIONS ALREADY INSTALLED.": GOTO 10028
10024 PRINT "LOADING EXTENSIONS..."
10026 PRINT  CHR$ (4);"BRUN /FUJI.APPLE/FUJIAPPLE"
10028 RETURN

10030 X = PEEK(1014)+PEEK(1015)*256
10040 X = X - 1
10050 C = PEEK(X)
10060 IF C = 0 OR C > 20 THEN 10140
10070 X = X - C
10080 A$=""
10090 FOR Y = 1 TO C+1: A$=A$+CHR$(PEEK(X)):X=X+1:NEXT Y
10100 B$ = "FUJIAMP"
10130 IF A$=B$ THEN R=1: GOTO 10150
10140 R=0
10150 RETURN


SAVE /FUJI.APPLE/ECHOSERVER.BAS

