
NEW

9 GOSUB 10000
10 GOTO 500

REM GET COLOR AT X,Y-1
18 LOCATE X,Y-1,C:COLOR C:PLOT X,Y:PLOT X+1,Y:RETURN

REM GET COLOR AT X-1, Y-1
19 LOCATE X-1,Y-1,C:GOTO 30

REM GET COLOR AT X-1, Y
20 LOCATE X-1,Y,CC:IF C=CC THEN C=0
30 COLOR C:PLOT X,Y:PLOT X+1,Y:GOTO 60
40 LOCATE X-1,Y,Z:IF Z=C THEN RETURN

45 COLOR C:FOR I=X-1 TO X+2:FOR J=Y-1 TO Y+1
50 PLOT I,J:NEXT J:NEXT I

60 FOR V=0 TO 10:NEXT V

61 & NSTATUS 0, BW, CN, ERR:IF BW>0 THEN GOSUB 18: & NINPUT 0,X,Y,TRIG:IF TRIG=0 THEN 1700
70 RETURN

REM START OF PROGRAM

500 DIM F(1),S(1,9)
501 LS=0

530 HTAB 1:VTAB 21:PRINT "":PRINT
531 PRINT "BE SURE TO FORWARD TCP PORT 6502.":PRINT
540 PRINT "HOSTNAME OR TO LISTEN"
550 INPUT H$
560 IF H$="" THEN LS=1
570 D$="N:TCP://"+H$+":6502/"
571 & NOPEN 0,12,0,D$
572 IF NOT LS THEN 600
575 HOME: PRINT "WAITING FOR CONNECTION..."
580 & NSTATUS 0,BW,CN,ER:IF CN = 0 THEN 580

581 PRINT "ACCEPTING CONNECTION.": &NACCEPT 0
582 TEXT:HOME


600 GC=0:GR:TEXT:HOME:HTAB 5: VTAB 21:PRINT "Type name, press RETURN"
601 IF NOT LS THEN 620

610 PRINT:PRINT "Player 1";:INPUT A$:IF A$="" THEN 610
611 & NWRITE 0,A$, LEN(A$): & NINPUT 0,B$
612 GOTO 700

620 PRINT:PRINT "Player 2";:INPUT B$:IF B$="" THEN 620
621 & NPRINT 0,B$: & NINPUT 0,A$
622 GOTO 700

625 F(0)=0:F(1)=0
630 IF A$="COMPUTER" THEN F(0)=1
640 IF B$="COMPUTER" THEN F(1)=1

700 GR:TEXT:HOME 
750 COLOR 3:FOR Y=1 TO 37
770 PLOT 17,Y:PLOT 18,Y:PLOT 19,Y
780 FOR X=20 TO 60 STEP 5:PLOT X,Y:NEXT X
790 PLOT 61,Y:PLOT 62,Y:PLOT 63,Y:NEXT Y
800 FOR X=17 TO 60
810 PLOT X,1:PLOT X,2
820 FOR Y=3 TO 35 STEP 4:PLOT X,Y:NEXT Y
830 PLOT X,36:PLOT X,37:NEXT X
850 FOR L=1 TO 6:READ X,Y,C:GOSUB 40:NEXT L:RESTORE
855 DATA 10,36,1,37,17,1,42,21,1,72,36,2,42,17,2,37,21,2

990 P=2
1000 P=3-P
1010 A=0:B=0:FOR M=22 TO 57 STEP 5:FOR N=5 TO 33 STEP 4
1020 LOCATE M,N,C:IF C=1 THEN A=A+1
1030 IF C=2 THEN B=B+1
1040 IF C=0 THEN X=M:Y=N
1050 NEXT N:NEXT M

1055 PRINT A$;:PRINT B$:PRINT " ";A;" ";:PRINT B;" "

1070 IF A+B=64 THEN 2000
1110 IF F(P-1)=0 THEN GOTO 1500
1120 GOSUB 5000
1130 IF Q>0 THEN 1730
1140 FOR L=0 TO 20
1143 PRINT "I PASS";:GOSUB 60:PRINT " ";:NEXT L:GOTO 1000
1500 C=P:GOSUB 20:GOSUB 20:GOSUB 19
1510 GET A$: N=ASC(A$)
1511 IF LS=0 AND P=1 THEN 1500
1512 IF LS=1 AND P=2 THEN 1500
1520 IF N=255 AND TRIG=1 AND JS=15 THEN 1500
1530 IF N=33 OR N=12 OR TRIG=0 THEN 1698
1540 IF N=30 THEN C=2:GOSUB 40:GOTO 1500
1550 IF N=31 THEN C=1:GOSUB 40:GOTO 1500
1560 IF N=10 THEN 1630
1570 X=X+5*((N=7 OR JS=7) AND X<57)-5*((N=6 OR JS=11) AND X>22)
1580 Y=Y+4*((N=15 OR JS=13) AND Y<33)-4*((N=14 OR JS=14) AND Y>5)
1581 IF N=33 OR N=12 THEN TRIG=0
1582 O$ = STR$(X)+","+STR$(Y)+","+STR$(TRIG):& NPRINT 0, O$
1590 GOTO 1500

1630 GOSUB 19:GOSUB 5000:IF Q=0 THEN 1000
1655 FOR I=0 TO 20
1656 PRINT " INVALID MOVE";:GOSUB 60:PRINT " ";
1657 NEXT I:GOTO 1010

1698 IF N=33 OR N=12 THEN TRIG=0
1699 O$ = STR$(X)+","+STR$(Y)+","+STR$(TRIG):& NPRINT 0, O$
1700 G=0:GOSUB 4000:IF R=0 THEN GOSUB 19:GOTO 1655
1730 G=1:GOSUB 4000
1780 GOTO 1000

2000 PRINT "To see scoreboard, press any key":GET A$
2040 S(0,GC)=A:S(1,GC)=B
2050 GR:TEXT:HOME:HTAB 1:VTAB 10:PRINT "GAME":HTAB 1: VTAB 12:PRINT A$:HTAB  1:VTAB 14:PRINT B$
2060 FOR X=0 TO GC:N=X*3+10:HTAB N: VTAB 10:PRINT X+1;:HTAB N: VTAB 12:PRINT S(0,X);:HTAB N: VTAB 14:PRINT S(1,X);:NEXT X
2100 PRINT :PRINT :PRINT "Same players go again? (Y-N)";:GET A$
2120 IF GC>7 AND N=89 THEN PRINT :PRINT "You have already played 9 games.";:FOR X=1 TO 999:NEXT X:GOTO 600
2130 IF N=89 THEN GC=GC+1:GOTO 700
2135 IF N<>78 THEN 2100
2140 GOTO 600
3000 GR:TEXT:HOME:PRINT "DISCONNECTED.":& NCLOSE 0:END


4000 R=0:SX=X:SY=Y
4005 LOCATE X-1,Y,C:IF C<>0 THEN RETURN
4010 FOR DX=-1 TO 1:FOR DY=-1 TO 1
4020 IF DX=0 AND DY=0 THEN 4130
4030 TR=0:X=SX:Y=SY
4050 X=X+DX*5:Y=Y+DY*4:LOCATE X,Y,C
4060 IF C=3-P THEN TR=TR+1:GOTO 4050
4070 IF C=P THEN R=R+TR:IF G=1 THEN 4100
4080 GOTO 4130

4100 X=X-DX*5:Y=Y-DY*4:C=P:GOSUB 40
4110 IF NOT (X=SX AND Y=SY) THEN 4100
4130 NEXT DY:NEXT DX:X=SX:Y=SY:RETURN

5000 Q=0:QX=0:QY=0:FOR M=0 TO 7:FOR N=0 TO 7
5020 X=M*5+22:Y=N*4+5
5030 LOCATE X,Y,C:IF C<>0 THEN 5090
5035 C=P:GOSUB 20:GOSUB 19
5040 G=0:GOSUB 4000
5080 IF R>Q THEN Q=R:QX=X:QY=Y
5090 NEXT N:NEXT M:X=QX:Y=QY:RETURN

7005 FOR X=0 TO 8:S(0,X)=0:S(1,X)=0:NEXT X
9999 GOTO 9999

10000 GOSUB 10030: IF R=1 THEN PRINT "EXTENSIONS ALREADY INSTALLED.": GOTO 100
10010 PRINT "LOADING EXTENSIONS..."
10020 PRINT  CHR$ (4);"BRUN /FUJI.APPLE/FUJIAPPLE":RETURN

10030 X = PEEK(1014)+PEEK(1015)*256
10040 X = X - 1
10050 C = PEEK(X)
10060 IF C = 0 OR C > 20 THEN 10140
10070 X = X - C
10080 A$=""
10090 FOR Y = 1 TO C+1: A$=A$+CHR$(PEEK(X)):X=X+1:NEXT Y
10100 B$ = "FUJIAMP"
10130 IF A$=B$ THEN R=1: GOTO 10150
10140 R=0
10150 RETURN

SAVE /FUJI.APPLE/REVERSI.BAS

